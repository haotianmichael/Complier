### PL/0扩展文法
```
<加法运算符> ::= +|-  
<乘法运算符> ::= *|/  
<字母> ::= _|a|b|...|X|Y|Z  
<关系运算符> ::= <|<=|>|>=|==|!=

<程序> ::= [<常量申明>][<变量申明>]{<有返回值函数定义> | <无返回值函数定义>}<主函数>

<常量申明>  ::= const<常量定义>;{const<常量定义>;}
<常量定义> ::= <类型标识符><标识符>=<整数>{, <标识符>=<整数>}   
<变量申明> ::= <变量定义>;{<变量定义>;}
<变量定义> ::= <类型标识符>(<标识符>|<标识符>'['<无符号整数>']'){, (<标识符>|<标识符>'['<无符号整数>']')}
<类型标识符> ::= int
<标识符> ::= <字母>{<字母>|<数字>}

<整数> ::= [+|-]<无符号整数>
<无符号整数> ::= <非零数字>{<数字>} | 0
<数字> ::= 0 | <非零数字>
<非零数字> ::= 1|2|...|8|9

<有返回值函数定义> ::= <类型标识符><标识符>'('<参数表>')''{'<复合语句>'}'
<无返回值函数定义> ::= void<标识符>'('<参数表>')''{'<复合语句>'}'
<参数表> ::= <参数>{, <参数>} | <空>
<参数> ::= <类型标识符><标识符>

<主函数> ::= int main'('')''{'<复合语句>'}'
<复合语句> ::= [<常量说明>][<变量说明>]<语句列>
<语句列> ::= {<语句>}
<语句> ::= <条件语句> | <循环语句> | '{'<语句列>'}' | <有返回值函数调用语句>; | <无返回值函数调用语句>; | <赋值语句>; | <读语句>; | <写语句>; | <空>; | <情况语句> | <返回语句>;
<条件语句> ::= if'('<条件>')'<语句>
<条件> ::= <表达式><关系运算符><表达式> | <表达式>
<循环语句> ::= while'('<条件>')'<语句>
<赋值语句> ::= <标识符>=<表达式> | <标识符>'['<表达式>']'=<表达式>
<情况语句> ::= switch'('<表达式>')''{'<情况表><缺省>'}'
<情况表> ::= <情况子语句>{<情况子语句>}
<情况子语句> ::= case<常量>: <语句>
<缺省> ::= default: <语句>|<空>
<有返回值函数调用语句> ::= <标识符>'('<值参数表>')'
<无返回值函数调用语句> ::= <标识符>'('<值参数表>')'
<值参数表> ::= <表达式>{, <表达式>} | <空>
<读语句> ::= scanf'('<标识符>{, <标识符>}')'
<写语句> ::= printf'('<表达式>')'
<返回语句> ::= return['('<表达式>')']
```


单个文件编译
包含全局变量(在main函数体之外定义)
常量: int整形常量
变量: int整形变量，int整形数组

